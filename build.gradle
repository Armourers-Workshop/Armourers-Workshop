buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

if (System.env.BUILD_NUMBER != null) {
    version = "$minecraftVersion-$modVersion.$System.env.BUILD_NUMBER"
} else {
    version = "$minecraftVersion-$modVersion"
}

group= "moe.plushie.armourers_workshop"
archivesBaseName = "Armourers-Workshop"

minecraft {
    version = "$project.minecraftVersion-$project.forgeVersion"
    runDir = "run"

    replaceIn "common/lib/LibModInfo.java"
    replace "@VERSION@", project.version
    
    if (!devBuild.toBoolean()) {
        replaceIn "common/lib/LibModInfo.java"
        replace "DEVELOPMENT_VERSION = true", "DEVELOPMENT_VERSION = false"
    }
    
    replaceIn "api/common/lib/LibApi.java"
    replace "@API_VERSION@", apiVersion
    
    mappings = "stable_39"
}

dependencies {
    // compile name: "buildcraft", version: "${buildcraftVersion}", classifier: "dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'moe/plushie/armourers_workshop/api/**'
    classifier = 'api'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives apiJar
    archives sourcesJar
    archives deobfJar
}
