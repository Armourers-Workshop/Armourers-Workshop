buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

def modVersion = "0.17.0"
def minecraftVersion = "1.7.10"

version = minecraftVersion + "-" + modVersion
group= "riskyken.armourersWorkshop"
archivesBaseName = "Armourers-Workshop"

minecraft {
    version = "1.7.10-10.13.0.1208"
    replaceIn "common/lib/LibModInfo.java"
    replace "@VERSION@", modVersion
    assetDir = "eclipse/assets"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
	from sourceSets.main.output
	include 'riskyken/armourersWorkshop/api/**' 
    classifier = 'api'
}

task devJar(type: Jar) {
    from sourceSets.main.allSource
	from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
	archives apiJar
	archives devJar
}
