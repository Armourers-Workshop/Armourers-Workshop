buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
	}
}

apply plugin: 'forge'

if (System.env.BUILD_NUMBER != null) {
    version = "$minecraftVersion-$modVersion.$System.env.BUILD_NUMBER"
} else {
    version = "$minecraftVersion-$modVersion"
}

group= "riskyken.armourersWorkshop"
archivesBaseName = "Armourers-Workshop"

minecraft {
    version = "$minecraftVersion-$forgeVersion"
    runDir = "eclipse"

    replaceIn "common/lib/LibModInfo.java"
    replace "@VERSION@", project.version

    replaceIn "api/common/lib/LibApi.java"
	replace "@API_VERSION@", apiVersion
}

dependencies {
	compile name: "buildcraft", version: "${buildcraftVersion}", classifier: "dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'riskyken/armourersWorkshop/api/**'
    classifier = 'api'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives apiJar
    archives sourcesJar
    archives deobfJar
}
